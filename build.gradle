plugins {
    id 'fabric-loom' version '1.5-SNAPSHOT'
    id 'me.modmuss50.mod-publish-plugin' version '0.4.5'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    mods {
        "enhancedworkbenches" {
            sourceSet("main")
        }
    }
}

repositories {
    maven {
        name = "isXander" // YACL
        url = "https://maven.isxander.dev/releases/"
    }
    maven {
        name = "SonaType" // YACL dependency
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name = "QuiltMC" // YACL dependency
        url = "https://maven.quiltmc.org/repository/release/"
    }
    maven {
        name = "TerraformersMC" // Mod Menu
        url = "https://maven.terraformersmc.com/releases/"
    }
    maven {
        name = "Shedaniel" // REI
        url = "https://maven.shedaniel.me/"
    }
    maven {
        name = "Modrinth" // Balm + Crafting Tweaks
        url = "https://api.modrinth.com/maven/"
        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "dev.isxander.yacl:yet-another-config-lib-fabric:${project.yacl_version}"
    include "dev.isxander.yacl:yet-another-config-lib-fabric:${project.yacl_version}"

    modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${project.rei_version}"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

    modImplementation "maven.modrinth:balm:${project.balm_version}"
    modImplementation "maven.modrinth:crafting-tweaks:${project.craftingtweaks_version}"

    modImplementation "maven.modrinth:quickbench:${project.quickbench_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}

publishMods {
    file = remapJar.archiveFile
    displayName = (project.mod_version as String)
    type = STABLE
    modLoaders.add("fabric")
    changelog =
    """
    """

    curseforge {
        projectId = "857513"
        projectSlug = "enhanced-workbenches"
        accessToken = providers.environmentVariable("CURSEFORGE_API_KEY")
        minecraftVersions.add(project.minecraft_version)

        requires {
            slug = "fabric-api"
        }
        optional {
            slug = "roughly-enough-items"
            slug = "crafting-tweaks-fabric"
        }
    }

    modrinth {
        projectId = "IhBYAM6m"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        minecraftVersions.add(project.minecraft_version)

        requires {
            slug = "fabric-api"
        }
        optional {
            slug = "rei"
            slug = "crafting-tweaks"
        }
    }


    discord {
        webhookUrl = providers.environmentVariable("CAFETERIA_MOD_RELEASE_WEBHOOK")
        username = "Enhanced Workbenches"
        avatarUrl = "https://cdn.modrinth.com/data/IhBYAM6m/40c25fa8d8faec5e1a1c65834d585d4d861c15db.png"

        content = changelog.map { "# Enhanced Workbenches " + (project.mod_version as String) + " has been released \n" + it}
    }
}